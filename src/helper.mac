ROUTINE helper
readAll(routine) public {
  zw routine
  quit "test"
}
max(&list, &ind, delim = ",") public {
  set max = -99999,ind=""
  if $d(list)\10 {
    set el=""
    for {
      set el=$o(list(el), 1, v)
      quit:el=""
      if $d(v),v>max {
        set max=v,ind=el
      } elseif '$d(v),el>max {
        set max=el
      }
    }
  } else {
    if '$lv(list) set list=$lfs(list, delim)
    for i=1:1:$ll(list) {
      set v = $lg(list, i)
      if v>max set max=v,ind=i
    }
  }
  return max
}
min(&list, &ind, delim = ",") public {
  set min = 99999999,ind=""
  if $d(list)\10 {
    set el=""
    for {
      set el=$o(list(el), 1, v)
      quit:el=""
      if $d(v),v<min {
        set min=v,ind=el
      } elseif '$d(v),el<min {
        set min=el
      }
    }
  } else {
    if '$lv(list) set list=$lfs(list, delim)
    for i=1:1:$ll(list) {
      set v = $lg(list, i)
      if v<min set min=v,ind=i
    }
  }
  return min
}
sum(list, delim = ",") public {
  set sum = 0
  if $d(list)\10 {
    set el=""
    for {
      set el=$o(list(el), 1, v)
      quit:el=""
      if $i(sum, v)
    }
  } else {
    if '$lv(list) set list=$lfs(list, delim)
    for i=1:1:$ll(list) {
      set v = $lg(list, i)
      if $i(sum, v)
    }
  }
  return sum
}
sort(list) public {
  for i=1:1:$l(list) {
    set c = $e(list, i)
    if $i(sort(c))
  }
  set res = ""
  set c = ""
  for {
    set c = $o(sort(c), 1, cnt)
    quit:c=""
    set res = res _ $tr($j("", cnt), " ", c)
  }
  return res
}
toHex(string) public {
  set res = ""
  for i=1:1:$l(string) {
    set val = $a(string, i)
    set hex = $zcvt($tr($j($zhex(val), 2), " ", "0"), "l")
    set res = res_hex
  }
  return res
}