ROUTINE Advent19.Day8
#include helper
Start() public {
  #; $$$inputAll(input)
  $$$inputFull(input)
  #; $$$input(lines)
  Do draw(input)

  Set len = $Length(input)
  Set min = 1e13
  Set minl = 0
  Set part1 = ""
  Set lastLayer = len \ (26 * 6)
  For i=1:1:lastLayer {
    Set $Listbuild(val0, val1, val2) = $$analyze(input, i, .image)
    If val0<min {
      Set min = val0
      Set minl = i
      Set part1 = val1 * val2
    }
  }
  Write !!
  ZWrite part1
}
draw(input) {
  Write #
  Set width = 25, height = 6
  Write !,*27,"[40m",$Justify("", width + 1),*27,"[0m"
  For y=1:1:height {
    Write !,*27,"[40m ",*27,"[0m"
    For x=1:1:width {
      Set layer = 1
      do {
        Set p = (layer - 1 * width * height) + (y - 1 * width + 1) + (x - 1)
        Set val = $Extract(input, p)
      } while val=2 && $Increment(layer)

      Write *27,$select(val = 1: "[42m ", 1: "[40m "),*27,"[0m"
      #; Write $select(val = 1: "#", 1: " ")
    }
  }
  Write !,*27,"[40m",$Justify("", width + 1),*27,"[0m"
}
analyze(input, layer = 1, &image) {
  Set width = 25, height = 6
  Set pos = layer - 1 * width * height
  For y=1:1:height {
    For x=1:1:width {
      Set val = $Extract(input, $Increment(pos))
      If $Increment(vals(val))
    }
  }
  Quit $Listbuild(vals(0), vals(1), vals(2))
}
