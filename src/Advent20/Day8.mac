ROUTINE Advent20.Day8
#include helper
Start() public {
  #; $$$inputAll(input)
  #; $$$inputFull(input)
  $$$input(input)
  If $$compile(.input, .errs)'=0 {
    ZWrite errs
    Quit
  }
  Set ts = $ZHorolog
  Write !,"Part1 = ",$$run^Advent20.Day8.gen()
  Write !,"time spent: ",$ZHorolog-ts

  Set ts = $ZHorolog
  Write !!,"Part2 = ",$$run^Advent20.Day8.gen(1)
  Write !,"time spent: ",$ZHorolog-ts
}
compile(&input,&errs) {
  Set code(0) = 1
  Set code(1) = "run(fix=0) public { set acc = 0 "
  For i=1:1:input {
    Set $ListBuild(op, val) = $$$match("(\w+) ([+-]\d+)", input(i))
    If (op = "acc") {
      Set codeLine = " set acc = acc "_val
    }
    If (op = "jmp") {
      Set codeLine = " if (fix="_i_") {} else { goto run+"_(i + val) _ " }"
    }
    If (op = "nop") {
      Set codeLine = " if (fix="_i_") { goto run+"_(i + val) _ " }"
    }
    Set codeLine = " if ($data(inLoop"_i_")) { quit:fix $$run(fix+1)  quit acc } set inLoop"_i_" = 1 " _ codeLine
    Set code(0) = code(0) + 1
    Set code(code(0)) = codeLine
  }
  Set code(0) = code(0) + 1
  Set code(code(0)) = " quit acc }"
  Set rtnName = $ZName _ ".gen"
  Set res = $Compile(code, 0, errs,,,,rtnName)
  Quit res
}