ROUTINE Advent18.Day10
#include helper
Start() [sky] public {
  kill sky
  $$$input(lines)
  for i=1:1:lines {
    set line = lines(i)
    set line=$tr(line, "<>=", ",")
    set line=$zstrip(line, "*AW")
    set $lb(,x,y,vx,vy) = $lfs(line)
    set points($i(points)) = $lb(x,y,vx,vy)
    set sky(x,y)=i
  }
  for time=1:1 {
    set height = $$move(.sky, .points)
    quit:height<10
  }
  do show(.sky)
  write !,"time = ",time
}
move(&sky, &points) {
  set (minX, minY) = 1e13
  set (maxX, maxY) = 0
  for i=1:1:points {
    set $lb(x,y, vx,vy) = points(i)
    kill sky(x,y)
    set x = x + vx
    set y = y + vy
    set sky(x, y) = i
    set $li(points(i), 1, 2) = $lb(x, y)
    if x>maxX set maxX = x
    if x<minX set minX = x
    if y>maxY set maxY = y
    if y<minY set minY = y
  }
  quit maxY-minY
}
show(&sky) public {
  set x = ""
  for {
    set x = $order(sky(x))
    quit:x=""
    write !
    set y = ""
    for {
      set y = $order(sky(x,y))
      quit:y=""
      write ?y,"*"
    }
  }
}