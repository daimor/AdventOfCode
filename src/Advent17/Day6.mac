ROUTINE Advent17.Day6
#include helper
start() public {
  $$$input(lines)
  while $$$nextLine(lines, input) {
    set input = $lfs(input, $c(9))
    set len = $ll(input)
    for pos=1:1 {
      set max=0,imax=0
      for i=1:1:len if $lg(input, i)>max set max=$lg(input, i),imax=i
      set $li(input, imax) = 0
      set i = imax + 1
      do {
        if i>len set i = 1
        set $li(input,i) = $li(input,i) + 1
      } while $i(i),$i(max, -1)
      quit:$d(history(input),first)
      set history(input)=pos
    }
    write !,"part1 = ", pos
    write !,"part2 = ", pos-first
  }
}
